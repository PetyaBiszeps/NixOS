# Niri global configuration
# Provides Niri setup config and module imports
# Feel free to add, remove and modify anything here

{ variables, pkgs, lib, ... }:
  let
    ide = variables.IDE or "zed";
    browser = variables.browser or "firefox";
    terminal = variables.terminal or "ghostty";
    desktopShell = variables.desktopShell or "";
    noctaliaStartup = lib.optionalString (desktopShell == "noctalia") ''
      spawn-at-startup "noctalia-shell"
    '';

    # Import keybinds
    keybindsModule = import ./keybinds.nix {
      inherit terminal browser ide desktopShell;
    };

    # Import styles
    styleModule = import ./styles.nix {};
in {
  home.packages = with pkgs; [
    niri
    grim
    slurp
    wl-clipboard
    xwayland-satellite
  ];

  # Generate Niri config.kdl
  xdg.configFile."niri/config.kdl".text = ''
    // Niri config
    // Generated by NixOS configuration

    ${noctaliaStartup}
    ${keybindsModule}
    ${styleModule}
  '';

  # Xwayland support
  systemd.user.services.xwayland-satellite = {
    Unit = {
      Description = "Xwayland outside Wayland";
      BindsTo = "graphical-session.target";
      After = "graphical-session.target";
    };

    Service = {
      Type = "notify";
      NotifyAccess = "all";
      Restart = "on-failure";
      StandardOutput = "journal";
      ExecStart = "${pkgs.xwayland-satellite}/bin/xwayland-satellite";
    };
    
    Install.WantedBy = ["graphical-session.target"];
  };
}